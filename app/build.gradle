import org.apache.tools.ant.taskdefs.condition.Os
//apply plugin: 'com.android.library'
apply plugin: 'com.android.application'

android {
    useLibrary 'org.apache.http.legacy'

    compileSdkVersion 26
    buildToolsVersion '27.0.3'

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }
    defaultConfig {
        applicationId "uk.ac.ucl.ndnocr"
        minSdkVersion 23
        targetSdkVersion 26
        versionCode 212
        versionName "2.1"
        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath false
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            jniDebuggable true
        }
    }

    sourceSets {
        main {
            java.srcDirs "src/main/java"
            res.srcDirs "src/main/res"
            jniLibs.srcDir 'src/main/libs'
            jni.srcDirs = [] //disable automatic ndk-build call
        }
        androidTest.setRoot('tests')
        androidTest.java.srcDirs = ['tests/src']
    }

    splits {
        abi {
            enable true // enable ABI split feature to create one APK per ABI
            universalApk false // don't generate an additional APK that targets all the ABIs
        }
    }

    // map for the version code
    // versionCode digit for each supported ABI, with 64bit>32bit and x86>armeabi-*
    project.ext.versionCodes = ['armeabi'    : 1,
                                'armeabi-v7a': 2,
                                'arm64-v8a'  : 3,
                                'mips'       : 5,
                                'mips64'     : 6,
                                'x86'        : 8,
                                'x86_64'     : 9]

    task copyNdnCxxHeaders(type: Copy) {
        from('src/main/jni/ndn-cxx/src') {
            include '**/*.hpp'
        }
        into 'build/generated/source/include/ndn-cxx'
    }

    // call regular ndk-build(.cmd) script from app directory
    task ndkBuild(type: Exec) {
        dependsOn copyNdnCxxHeaders
        def args = [getNdkBuildCmd(), '-C', file('src/main').absolutePath]

        if (System.env.NDK_BUILD_PARALLEL != null) {
            args.add("-j" + System.env.NDK_BUILD_PARALLEL)
        } else {
            args.add("-j" + Runtime.runtime.availableProcessors())
        }

        if (System.env.NDK_BUILD_ABI != null) {
            args.add("APP_ABI=" + System.env.NDK_BUILD_ABI)
        }

        if (System.env.NDK_DEBUG != null) {
            args.add("NDK_DEBUG=1")
        }
        commandLine args
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }

    task cleanNative(type: Exec) {
        commandLine getNdkBuildCmd(), '-C', file('src/main').absolutePath, 'clean'
    }

    clean.dependsOn cleanNative

    /*externalNativeBuild {
        cmake {
            path '../../../ndn/ccn-lite/src/ccnl-android/native/CMakeLists.txt'
        }
    }*/
}

def getNdkBuildCmd() {
    def ndk_build = getNdkDir() + "/ndk-build"
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        ndk_build += ".cmd"
    }

    return ndk_build
}


def getNdkDir() {
    if (System.env.ANDROID_NDK_ROOT != null)
        return System.env.ANDROID_NDK_ROOT

    Properties properties = new Properties()
    properties.load(project.rootProject.file('local.properties').newDataInputStream())

    def ndk_dir = properties.getProperty('ndk.dir', null)
    if (ndk_dir == null) {
        throw new GradleException("NDK location not found. Define location with ndk.dir in the local.properties file or with an ANDROID_NDK_ROOT environment variable.")
    }
    return ndk_dir
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
}

dependencies {
   // compile 'com.github.TeamNewPipe:NewPipeExtractor:b9d0941'
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile('com.intel.jndn.management:jndn-management:1.1.0') {
        exclude group: 'net.named-data', module: 'jndn'
    }
    compile files('libs/jndn-0.15.jar')
    //compile files('libs/sqlite-jdbc-3.21.0.jar')
    //compile 'org.sqldroid:sqldroid:1.0.3'
    compile 'net.grandcentrix.tray:tray:0.12.0'
    compile 'com.google.firebase:firebase-messaging:11.0.4'
    compile('com.firebase:firebase-jobdispatcher-with-gcm-dep:0.6.0') {
        exclude module: "play-services-gcm"
    }
    compile 'com.android.support:appcompat-v7:27.1.0'
    compile 'com.android.support:support-v4:27.1.0'
    compile 'com.android.support:design:27.1.0'
    compile 'com.android.support:recyclerview-v7:27.1.0'
    compile 'com.android.support:preference-v14:27.1.0'
    compile 'com.google.firebase:firebase-storage:11.0.4'
    compile 'com.google.firebase:firebase-auth:11.0.4'
    compile 'net.named-data.jndn-xx:jndn-xx-util:0.0.1'
    compile 'com.google.android.gms:play-services:11.0.4'
    compile 'org.slf4j:slf4j-api:1.7.12'
    compile 'joda-time:joda-time:2.7'
    //compile files('libs/netty-buffer-4.1.0.Beta5.jar')
    //compile files('libs/netty-common-4.1.0.Beta5.jar')
    compile 'com.google.protobuf:protobuf-java:2.6.1'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.5'
    compile 'de.hdodenhof:circleimageview:2.1.0'
    compile 'com.github.nirhart:parallaxscroll:1.0'
    compile 'com.nononsenseapps:filepicker:3.0.1'
    compile 'com.google.android.exoplayer:exoplayer:r2.5.4'
    compile 'io.reactivex.rxjava2:rxjava:2.1.2'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
    compile 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    compile 'android.arch.persistence.room:runtime:1.0.0-alpha8'
    compile 'android.arch.persistence.room:rxjava2:1.0.0-alpha8'
    annotationProcessor 'android.arch.persistence.room:compiler:1.0.0-alpha8'
    compile 'frankiesardo:icepick:3.2.0'
    provided 'frankiesardo:icepick-processor:3.2.0'
   // implementation project(':kbapp')
}

//apply plugin: 'com.android.application'
//apply plugin: 'com.google.gms.google-services'


Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

if (properties['keystore'] == null || properties['keystore.password'] == null ||
        properties['keystore.key.alias'] == null || properties['keystore.key.password'] == null) {
    println("Cannot sign .apk: specify keystore, keystore.password, keystore.key.alias, and keystore.key.password in local.properties.")
} else {
    android {
        signingConfigs {
            release {
                storeFile file(properties['keystore'])
                storePassword properties['keystore.password']
                keyAlias properties['keystore.key.alias']
                keyPassword properties['keystore.key.password']
            }
        }
        buildTypes {
            release {
                signingConfig signingConfigs.release
            }
        }
    }
}

if (properties['google-play'] != null) {
    apply plugin: 'net.named-data.playground.play'

    play {
        track = 'alpha'
        jsonFile = file(properties['google-play'])
    }
}

